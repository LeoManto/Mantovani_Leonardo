System parkingservice
 
Request reqenter : reqenter(V)
Reply slotsnum : slotsnum(SLOTNUM)

Request carenter : carenter(V) 
Reply receipt  : receipt(TOKENID)

Dispatch pickup : pickup(tokenID)

Event timeout 			: timeout(V)
Event sensor 			: sensor(areaFree)
Event carindoorarrival : cia(V)
Event caroutdoorarrival : coa(V)
Event carwithdrawn		: cw(V)
Event weightsensor		: weight(W)
Event alarm 			: timeout(alarm)


Context ctxParkingservice  ip [host="localhost" port=8052]

CodedQActor outsonar context ctxParkingservice className "sonarSimulator"
CodedQActor weightsensor context ctxParkingservice className "weightsensorSimulator"


/* 
 * ------------------------------------------------------------------
 *   
 * ------------------------------------------------------------------
*/  
  
 
QActor client context ctxParkingservice{
	[# val tester = `it.unibo`.parkingmanagerservice.test.utils.Tester
		var SLOTNUM = 0
		var TOKENID = 0 // sarà direttamente il num dello slot assegnato //
	#]
	State s0 initial{
		println("Client mock simulation START | CLIENT")
	} Transition t0
					whenTime 4000 -> requestToEnter
	 
	State requestToEnter{
		println("client notify his interest in entering | CLIENT")
		[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.reqToEnter) #]
		request parkingmanagerservice -m reqenter : reqenter(client)
	} Transition t0 
					whenTime 3000 -> noresponse  //se non si gestisce entro 3 sec, il cliente non entra
					whenReply slotsnum -> afterslotnum
	
	
	State noresponse {
		[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.noResponse) #]
		println("No SLOTNUM received, System not correctly work")
	}
	
	
	State noentry{
		[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.noEntry) #]
		println("No Free Slot, I can't entry")
	} 
	
	
	State afterslotnum {
		onMsg(slotsnum : slotsnum (SLOTNUM)){
			[# SLOTNUM = payloadArg(0).toInt() #]	
			[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.carToIndoor)
			#]
			println("SLOTNUM = $SLOTNUM | CLIENT" )
			if [# SLOTNUM > 0 #]{		
				request parkingmanagerservice -m carenter : carenter(V) 
			}
		} 
	} Goto cartoindoor if[# SLOTNUM > 0 #] else noentry
	
	
	State cartoindoor{	
	}Transition t0 
					whenReply receipt -> afterreceipt
	

	State afterreceipt{
		onMsg(receipt : receipt (TOKENID)){
			[# TOKENID = payloadArg(0).toInt() #]
			updateResource [# "TOKENID" #]
			println("client's TOKENID is $TOKENID | CLIENT")
			[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.afterReceipt) 
			   tester.setToken(TOKENID)
			#]
		}
	} 
	Transition t0
					whenTime 10000 -> reqpickup  //10 secs prima di chiedere il pickup

	State reqpickup{
		/*==========[acceptOUT]==========*/
		println("client notify his interest in picking his car | CLIENT")
		forward parkingmanagerservice -m pickup : pickup($TOKENID)
		[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.reqPickup) #]
	} Transition t0 
					whenEvent caroutdoorarrival -> pickupcar
	
	
	State pickupcar{
		delay 2000 //in 2 secondi porta via l'auto da OUTDOOR
		emit carwithdrawn : cw(bye)
		[# tester.setclientstate(`it.unibo`.parkingmanagerservice.test.utils.ClientState.pickupCar) #]
	}
	
}

 
QActor parkingmanagerservice context ctxParkingservice{
	[#
	lateinit var weightSensorActor : ActorBasic
	lateinit var outSonarActor : ActorBasic
	
	var indoorFree 	  = true
	var outdoorFree	  = true
	var trolleyStatus = true
	var SLOTNUM 	  = 0
	#]
	
	State s0 initial{
		[#
		outSonarActor     = sysUtil.getActor("outsonar")!!
		weightSensorActor = sysUtil.getActor("weightsensor")!!
		#]   
	println("Park System START | SERVICE")	
	}
	Goto ready
	
	State ready{
		println("waiting for client | SERVICE")
	}
	Transition t0 	whenMsg pickup   -> handleToken
					whenRequest reqenter -> acceptin //non c'è controllo sui posti liberi per assunzione
	
	State acceptin{
		/*==========[acceptIN]==========*/
		[# SLOTNUM = `it.unibo`.parkingmanagerservice.test.utils.Tester.getSlot() #] //Per ora è settato manualmente in fase di test
		updateResource [# "SLOTNUM" #]
		/*==========[informIN]==========*/
		replyTo reqenter with slotsnum : slotsnum ($SLOTNUM)
		updateResource [# "slotsnum : slotsnum ($SLOTNUM)" #]
		/*==========[moveToIn]==========*/
		println("Trolley is moving to Indoor | SERVICE")
	} Transition t0
					whenRequest carenter -> carenter
					
	State carenter{
		[# indoorFree = false #]	
		emit carindoorarrival : cia(car_arrived)  //questo lo deve catturare il weightsensor		
	} Transition t0
				whenEvent weightsensor -> moveToSlotIn
				
	State moveToSlotIn{
		onMsg(weightsensor : weight(W)){
			[# 
				var peso = payloadArg(0)
				println("Weight: " + peso)
			#]
			}
		/*==========[moveToSlotIn]==========*/
		println("Trolley moves from entrance to slot $SLOTNUM | SERVICE")
		[# indoorFree = true #]
	} Transition t0
			whenTime 4000 -> receipt //simuliamo lo spostamento del Trolley
					
					
	State receipt{ 
		[# var TOKENID = SLOTNUM #]	//si elabora un token-id (in questo caso è lo SLOTNUM usato prima)
		/*==========[receipt]==========*/
		replyTo carenter with receipt : receipt($TOKENID)
		updateResource [# "receipt : receipt($TOKENID)" #]
		emit carindoorarrival : cia(car_arrived)  //questo lo deve catturare il weightsensor
		/*==========[moveToHome]==========*/
		println("Trolley moves to Home | SERVICE")
	} Transition t0 
					whenTime 1000 -> ready  //simuliamo il ritorno ad Home del Trolley
	
	
	State handleToken{
		onMsg (pickup : pickup(tokenID)){
			/*==========[findSlot]==========*/
				[# var tokenIN = payloadArg(0) #]} //tokenIN è il token fornito dal client
		//elaborazioni sul tokenIN per ottenere lo slotnum corrispondente(non in questo modello)
		
		println("checking Trolley status | SERVICE")
	}  
	Transition t0
				whenTime 500 -> picking  //simuliamo l'elaborazione del tokenIN
	
	 
	State picking{	 	
		println("Trolley picking car | SERVICE")
		/*==========[moveToSlotOut]==========*/
		/*==========[moveToOut]==========*/
		delay 4000
		println("Car is in Outdoor area | SERVICE")
		/*==========[moveToHome]==========*/
		println("Trolley moves to Home | SERVICE")
		emit caroutdoorarrival : coa(car_outdoor)  //deve essere percepito dal sensore
		[# outdoorFree = false #]
	}
	Transition t0 	whenEvent carwithdrawn  -> withdrawn
					whenEvent timeout -> timeout
 
	
	State withdrawn{
		[# outdoorFree = true #]
		println("Car withdrawn!")
	} 
	Goto ready
	
	State timeout{
		println("%%%% TIMEOUT %%%%" )
		emit alarm : timeout(alarm)		
	}
		
}



